#!/usr/bin/perl -w

# $Id: dudl-storhave,v 1.6 2008-12-28 11:39:23 bj Exp $

#
# Copyright (c) 2008 Rainer Clasen
#
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#

use strict;
use Dudl::DB;
use Getopt::Long;

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"help|h!"	=> \$wanthelp,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
usage: $0 <opt> <what> <arg>
options:
 --help                  this short usage information.
what:
 dir, fname, file, all   field to search for regexp <arg>
 sql, select             SQL where Statement to search for
EOF
	exit 0;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}


=pod

=head1 NAME

dudl-storhave - find files in file database

=head1 SYNOPSIS

dudl-storhave <what> <arg>

=head1 DESCRIPTION

find files in file database.

<arg> depends on <what>. Valid choices are:

=over 4

=item B<dir>|B<fname> <regexp>

find files where regexp matches specified field.

=item B<file> <regexp>

find files where regexp matches the full path incl. filename.

=item B<all>|B<any> <regexp>

find files where regexp matches I<dir> or I<fname>.

=item B<sql>|B<select> <where>

find files where the specified SQL I<where> statements matches.

=back

=head1 EXAMPLES

dudl-storhave any foo

dudl-storhave fname bar

dudl-storhave sql "fname ~* 'foo' AND dir ~* 'bar' AND collection = 'sl'"

=head1 SEE ALSO

I<Dudl::Overview>

=head1 AUTHOR

Rainer Clasen

=cut

my @attribs = qw( dir fname );
my $filesql = "TRIM( both ' ' FROM collection ) || ".
	"colnum || '/' || dir || '/' || fname";

my $what=lc shift || die "missing 'what' argument: any|file|sql|..";
my $pattern = join( " ", @ARGV ) || die "missing pattern";

my $dudl = Dudl::DB->new;
my $db = $dudl->db;

$pattern = $db->quote( $pattern, DBI::SQL_CHAR );

my $opt_debug=0;
my $where;
if( $what eq "all" || $what eq "any" ){
	foreach( @attribs ){
		$where .= " OR " if $where;
		$where .= "( $_ ~* $pattern )";
	}

} elsif( $what eq "file" ){
	$where .= "($filesql) ~* $pattern";

} elsif( $what eq "sql" || $what eq "select" ){
	$where = join " ", @ARGV;

} else {
	my $found = 0;
	foreach( @attribs ){
		if( $_ eq $what ){
			$where .= "$what ~* $pattern";
			$found++;
			last;
		}
	}
	if( $found ){
		die "invalid 'what' argument";
	}
}


print "SELECT ... WHERE $where\n" if $opt_debug;

my $query =
"SELECT ".
	"collection, ".
	"colnum, ".
	"dir, ".
	"fname ".
"FROM ".
	"stor_file f INNER JOIN ".
		"stor_unit u ".
		"ON u.id = f.unit_id ".
"WHERE ".
	"NOT broken AND ".
	"( $where ) ".
"ORDER BY ".
	"u.collection, ".
	"u.colnum, ".
	"f.dir, ".
	"f.fname";

my $sth = $db->prepare( $query );
if( ! $sth ){
	die $db->errstr ."\nquery: $query\n";
}

my $res = $sth->execute;
if( ! $res ){
	die $sth->errstr ."\nquery: $query\n";
}

my(
	$col,
	$colnum,
	$dir,
	$fname
);
$sth->bind_columns( \(
	$col,
	$colnum,
	$dir,
	$fname
));

while( defined $sth->fetch ){
	$col =~ s/\s+$//;
	$colnum = sprintf "%04d", $colnum;
	print $col, "/", $col, $colnum,"/",
		$dir, ($dir ? "/" : ""), $fname, "\n";
}
$sth->finish;

