#!/usr/bin/perl -w

# $Id: Archive.pm,v 1.5 2007-01-27 16:19:46 bj Exp $

=pod

=head1 NAME

Dudl::Job::Archive - file reader/writer for archive jobfiles

=head1 DESCRIPTION

Dudl::Job::Archive is a customized version of Dudl::Job::Base. It allows
some more attributes than the plain ::Base version does. 

Archive jobfiles are usually generated by dudl-rename and later processed
by dudl-scan.

=head1 BUGS

This manpage should document the differences from Dudl::Job::Base.

=head1 SEE ALSO

L<Dudl::Job::Base>, L<dudl-rename(1)>, L<dudl-scan(1)>.

=head1 AUTHOR

Rainer Clasen

=cut

package Dudl::Job::Archive;

use strict;
use Carp qw( :DEFAULT cluck);
use Dudl::Job::Base;

BEGIN {
	use Exporter ();
	use vars	qw($VERSION @ISA @EXPORT @EXPORT_VAR @EXPORT_OK %EXPORT_TAGS);

	# set the version for version checking
	$VERSION	= 1.00;
	@ISA		= qw( Dudl::Job::Base );

	# exported by default:
	@EXPORT_VAR	= qw();
	@EXPORT		= ( qw(), 
			@EXPORT_VAR );
	
	# shortcuts for in demand exports
	%EXPORT_TAGS	= ( );     # eg: TAG => [ qw!name1 name2! ],

	# your exported package globals go here,
	# as well as any optionally exported functions
	@EXPORT_OK	= ( qw(),
			@EXPORT );
}
use vars	@EXPORT_VAR;

# non-exported package globals go here

# initialize package globals, first exported ones

sub album_valid {
	my $self = shift;
	my $cur = shift || $self->{album};

	my $err = 0;
	if( ! $cur->{year} ){
		$self->bother( "no year set");
		#$err++; # non-fatal, ignore

	} elsif( $cur->{year} < 100 ){
		$self->bother( "year lacks century");
		$err++;

	}

	$self->SUPER::album_valid( $cur ) || $err++;

	return !$err;
}

sub file_valid {
	my $self = shift;
	my $cur = shift || $self->{file};

	my $err = 0;
	if( ! $cur->{mp3} ){
		$self->bother( "no mp3 name for file");
		$err++;
		
	}

	$self->SUPER::file_valid( $cur ) || $err++;

	return !$err;
}

sub file_key {
	my $self = shift;
	my $key = shift;
	my $val = shift;

	my $cur = $self->{file};

	if( $key eq "mp3" ){
		$cur->{$key} = $val;
		return 1;
	
	}

	return $self->SUPER::file_key( $key, $val );
}

sub write_file {
	my $self = shift;
	my $fh = shift;
	my $fil = shift;

	print $fh "file_mp3 \t". ($fil->{mp3} || "") ."\n";
	$self->SUPER::write_file( $fh, $fil );
}

1;
