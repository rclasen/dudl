#!/usr/bin/perl -w

# $Id: dudl-mushave,v 1.4 2008-12-28 11:39:23 bj Exp $

#
# Copyright (c) 2008 Rainer Clasen
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#

use strict;
use Getopt::Long;
use Dudl::DB;

=pod

=head1 NAME

dudl-mushave - search music database

=head1 SYNOPSIS

dudl-mushave [<opts>] [<what> ...]

=head1 DESCRIPTION

searches the music Database. Searches for all <what> that match all
supplied regexps.

=over 4

=item <what>

What information are you interested in? Pick one or many from:

=over 4

=item artist

=item album

=item title

=item genre

=back

=back

=head1 OPTIONS

=over 4

=item B<--help>

short usage information.

=cut

my $opt_help = 0;

=pod

=item B<--artist>=<artistRE>

regexp to match against artist name.

=cut

my $opt_artist = "";

=pod

=item B<--album>=<albumRE>

regexp to match agains album name.

=cut

my $opt_album = "";

=pod

=item B<--title>=<titleRE>

regexp to match against title name

=cut

my $opt_title = "";

=pod

=item B<--debug>

show debug information.

=cut

my $opt_debug = 0;
my $needhelp = 0;

my $want_artist = 0;
my $want_album = 0;
my $want_title = 0;
my $want_genre = 0;

sub usage {
	my $fh = shift;
	print $fh "usage: $0 [opts] <what>
search musik database
 --artist <regexp>    
 --album <regexp>    
 --title <regexp>    
 --help
<what>:
 artist
 album
 title
 genre
";
}


if( ! GetOptions(
	"help|h!"		=> \$opt_help,
	"artist|a=s"		=> \$opt_artist,
	"album|l=s"		=> \$opt_album,
	"title|t=s"		=> \$opt_title,
	"debug|d!"		=> \$opt_debug,
) ){
	$needhelp ++;
}

my $want = 0;
if( ! @ARGV ){
	push @ARGV, "most";
}

foreach my $a ( @ARGV ){
	if( $a =~ /^artist$/i ){
		$want_artist++;
		$want++;

	} elsif( $a =~ /^album$/i ){
		$want_album++;
		$want++;

	} elsif( $a =~ /^title$/i ){
		$want_title++;
		$want++;

	} elsif( $a =~ /^genre$/i ){
		$want_genre++;
		$want++;

	} elsif( $a =~ /^most$/i ){
		$want_artist++;
		$want_album++;
		$want_genre++;
		$want++;

	} elsif( $a =~ /^all$/i ){
		$want_artist++;
		$want_album++;
		$want_title++;
		$want_genre++;
		$want++;

	} else {
		$needhelp ++;
		print STDERR "invalid argument to what\n";
	}
}

if( ! $want ){
	print STDERR "nothing wanted!\n";
	$needhelp++;
}

if( $opt_help ){
	&usage( \*STDOUT );
	exit 0;
}

if( $needhelp ){
	&usage( \*STDERR );
	exit 1;
}

my $dudl = new Dudl::DB;
my $db = $dudl->db;


my $query = "";

if( $want_artist ){

	$query = "SELECT DISTINCT
		a.id, 
		a.nname
	FROM
		mus_artist a 
	";

	$query .= ", mus_album al " if $opt_album;
	$query .= ", stor_file t " if $opt_title;

	my @w;
	push @w, "a.id = t.artist_id", 
		"t.title ~* '$opt_title'" if $opt_title;
	push @w, "a.id = al.artist_id",
		"al.album ~* '$opt_album'" if $opt_album;
	push @w, "al.id = t.album_id" if $opt_title && $opt_album;
	push @w, "a.nname ~* '$opt_artist'" if $opt_artist;

	$query .= "WHERE ". join " AND ", @w if @w;

	print "\nartists:\n";
	&query( $db, $query );
}

if( $want_genre ){
	my @s =( "genres" );
	push @s, "a.nname", "a.id" if $opt_artist;

	$query = "SELECT
		count(*), mserv_tags(t.id) as ". join( ", ", @s ) ."
	FROM
		stor_file t 
	";

	$query .= ", mus_artist a " if $opt_artist;

	my @w;
	push @w, "a.id = t.artist_id", 
		"a.nname ~* '$opt_artist'" if $opt_artist;
	push @w, "t.title ~* '$opt_title'" if $opt_title;

	$query .= "WHERE ". join " AND ", @w if @w;
	$query .= "GROUP BY ". join( ", ", @s );

	print "\ngenres:\n";
	&query( $db, $query );
}

if( $want_album ){

	$query = "SELECT DISTINCT
		al.id,
		al.album,
		al.publish_date,
		a.nname
	FROM
		mus_album al,
		mus_artist a
	";

	$query .= ", stor_file t " if $opt_title;

	my @w;
	push @w, "al.artist_id = a.id";
	push @w, "a.nname ~* '$opt_artist'" if $opt_artist;
	push @w, "al.album ~* '$opt_album'" if $opt_album;
	push @w, "t.album_id = al.id", 
		"t.title ~* '$opt_title'" if $opt_title;

	$query .= "WHERE ". join " AND ", @w if @w;

	print "\nalbums:\n";
	&query( $db, $query );
}

if( $want_title ){

	$query = "SELECT DISTINCT
		t.id,
		t.title,
		a.nname
	FROM
		stor_file t,
		mus_artist a
	";

	$query .= ", mus_album al " if $opt_album;

	my @w;
	push @w, "t.artist_id = a.id";
	push @w, "a.nname ~* '$opt_artist'" if $opt_artist;
	push @w, "t.title ~* '$opt_title'" if $opt_title;
	push @w, "t.album_id = al.id", 
		"al.album ~* '$opt_album'" if $opt_album;

	$query .= "WHERE ". join " AND ", @w if @w;

	print "\ntitles:\n";
	&query( $db, $query );
}



sub query {
	my $db = shift;
	my $query = shift;

	print "query: ", $query, "\n" if $opt_debug;

	my $sth = $db->prepare( $query );
	if( ! $sth ){
		die $db->errstr ."\nquery: $query\n";
	}

	my $res = $sth->execute;
	if( ! $res ){
		die $sth->errstr ."\nquery: $query\n";
	}

	my $r;
	while( defined( $r = $sth->fetchrow_arrayref ) ){
		foreach my $c ( @$r ){
			print "$c	";
		}
		print "\n";
	}	
	$sth->finish;
} 

=pod

=back

=head1 EXAMPLES

dudl-mushave --artist foo album

dudl-mushave --artist foo.fig --album color genres

=head1 SEE ALSO

I<Dudl::Overview>

=head1 AUTHOR

Rainer Clasen

=cut


