#!/usr/bin/perl -w


use strict;
use Dudl::DB;
use Getopt::Long;

my $wanthelp;
my $needhelpt;

if( ! GetOptions(
	"help|h!"	=> \$wanthelp,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
usage: $0 <opt>
 find albums without a publish_date and prompt user to complete this
options:
 --help                  this short usage information.
EOF
	exit 0;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}


=pod

=head1 NAME

dudl-musyearadd - find albums without a publish_date and prompt user to complete this

=head1 SYNOPSIS

dudl-musyearadd 

=head1 DESCRIPTION

finds albums without a publish_date and prompt user to complete this.

=head1 SEE ALSO

I<Dudl::Overview>

=head1 AUTHOR

Rainer Clasen

=cut

$|=1;

my $dudl = new Dudl::DB;
my $db = $dudl->db;

my $query = "SELECT
	a.id,
	ar.nname AS artist,
	a.album
FROM
	mus_album a INNER JOIN 
		mus_artist ar ON a.artist_id = ar.id
WHERE
	a.publish_date ISNULL
	AND ar.nname != 'VARIOUS'
ORDER BY
	artist,
	album
";
my $lst = $db->prepare( $query ) or die $db->errstr."\nquery: $query";


my( $id, $artist, $album );
my $res = $lst->execute or die $db->errstr."\nquery: $query";
$lst->bind_columns( \( $id, $artist, $album ));

LST: while( defined $lst->fetch ){
	my $date;
	IN: while(1){
		print "\n";
		print "$id $artist: $album\n";

		print " n - for next\n";
		print " q - to quit\n";
		print "date: ";
		$date = <STDIN>;
		chomp $date;

		next LST if $date =~ /^n$/i;
		last LST if $date =~ /^q$/i;

		$date .= "-1" if $date =~ /^\d+-\d+$/;
		$date .= "-1-1" if $date =~ /^\d+$/;
		last if $date =~ /^\d\d\d\d-\d+-\d+$/;

		print STDERR "invalid date\n";
	}

	$query = "UPDATE mus_album SET publish_date = '$date' where id=$id";
	$db->do( $query ) or warn $db->errstr."\nquery: $query";
	$dudl->commit;
}

