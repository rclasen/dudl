#! /usr/bin/perl -w

# $Id: dudl-dotest,v 1.8 2007-03-21 10:19:33 bj Exp $

use strict;
use Getopt::Long;
use Dudl::DB;
use Dudl::Misc;
use Dudl::StorUnit;

# TODO: automagically run dudl-musimport 
 
=pod

=head1 NAME

dudl-dotest - add MP3 directory to testing workspace

=head1 SYNOPSIS

dudl-dotest [<opts>] <dir> ...

=head1 DESCRIPTION

TODO: man desc


=head1 OPTIONS

=over 4

=item B<--prefix>=<string>

prefix new directory names with speciefied string.

=cut

my $opt_prefix = "";

=item B<--nodb>

don't try to add files to DB

=cut

my $opt_db = 1;

=item B<--help>

display short usage info.

=cut

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"help!"		=> \$wanthelp,
	"db!"		=> \$opt_db,
	"prefix=s"	=> \$opt_prefix,
)){
	$needhelp++;
}

if( $#ARGV < 0 ){
	print STDERR "missing directory\n";
	$needhelp++;
}


if( $wanthelp ){
	print <<EOF;
usage: $0 <opts> <dir> ...
 add MP3 directory to testing workspace
options:
 --help                  this short usage information.
 --nodb                  don't try to add files to DB
 --prefix <string>       prefix new directory names with speciefied string
EOF
	exit 0;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}

my @jobnames = qw( /ren /TRACKS.dudl_rename /TRACKS.dudl_archive);

my $dudl = new Dudl::DB;
my $nam = $dudl->naming;
my $tdir = $dudl->conf("workpath"). "/2test";

my $tunit = Dudl::StorUnit->load_step( dudl => $dudl, step => 2 );

if( $opt_prefix && $opt_prefix !~ /-$/ ){
	$opt_prefix .= "-";
}

foreach my $src ( @ARGV ){
	unless( -d $src ){
		print STDERR "skipping non-dir: $src\n";
		next;
	}

	if( &samedir( &parentdir( $src ), $tdir )){
		print STDERR "already in testing workspace: $src\n";
		next;
	}

	$src =~ /([^\/]+)\/*$/;
	my $dst = $tdir . "/". $nam->fnormalize( lc $opt_prefix.$1 );
	system( "mv",  $src, $dst ) == 0
		or die "mv failed: $1";

	my $found;
	foreach my $f ( @jobnames ){
		if( -f "$dst$f" ){
			$found++;
			last;
		}
	}

	$found || &mkren( $dst ) 
		or die "creating jobfile failed";
}

system( "dudl-dbavcheck", $tunit->val("id") ) if $opt_db;

sub mkren {
	my( $dst ) = @_;

	# TODO: re-use playlists (m3u, pls)

	# TODO: don't fork external find
	# TODO: perlify jobfile generation
	# TODO: warn when no feasable files were found
	system( "cd '$dst' && find . -type f -iname \\*.mp3 | sort | ( printf \"TODO: edit\\n# TODO: year\\n\"; dudl-rengen - ) > ren 2>/dev/null") == 0
		or return;

	return 1;
}

=pod

=head1 EXAMPLES

dudl-dotest --prefix metallica- master.of.puppets/

=head1 SEE ALSO

I<Dudl::Overview>

=head1 AUTHOR

Rainer Clasen

=cut



