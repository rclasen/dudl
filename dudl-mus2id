#!/usr/bin/perl -w


use strict;
use Dudl::DB;
use MP3::Tag;
use Getopt::Long;

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"help|h!"	=> \$wanthelp,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
usage: $0 <opt>
 set ID3v1 tags according to data from music database
options:
 --help                  this short usage information.
EOF
	exit 0;
}

if( $needhelp ){
	print STDERR "use --help for usage info\n";
	exit 1;
}


=pod

=head1 NAME

dudl-mus2id - set ID3v1 tags according to data from music database

=head1 DESCRIPTION

This script takes your tediously maintained music Database and sets the
ID3v1 Tags of those files you've already included.

You may want to run this on a periodic basis.

=head1 SEE ALSO

I<Dudl::Overview>,
I<dudl-mus2job>

=head1 AUTHOR

Rainer Clasen

=cut

my $dudl = new Dudl::DB;
my $db = $dudl->db;

my $query = "SELECT
	stor_filename(u.collection, u.colnum, f.dir, f.fname) AS path,
	f.album_pos,
	f.title,
	ta.nname AS artist,
	a.album,
	EXTRACT(year FROM a.publish_date) AS year
FROM
	stor_file f 
		INNER JOIN stor_unit u ON f.unit_id = u.id
		INNER JOIN mus_album a ON f.album_id = a.id
		INNER JOIN mus_artist ta ON f.artist_id = ta.id
WHERE 
	ta.nname != 'UNKNOWN'
	AND a.album != 'UNSORTED'
	AND NOT u.autoscan
ORDER BY
	path
";
my $sth = $db->prepare( $query ) or die $db->errstr."\nquery: $query";


my( $path, $pos, $title, $artist, $album, $year );
my $res = $sth->execute or die $db->errstr."\nquery: $query";
$sth->bind_columns( \( $path, $pos, $title, $artist, $album, $year ));

while( defined $sth->fetch ){
	my $fname = $dudl->conf("cdpath") . "/$path";
	my $tt = new MP3::Tag( $fname ) || next;
	my $t = $tt->new_tag( 'ID3v1' ) || next;

	$t->track( $pos );
	$t->song( $title );
	$t->artist( $artist );
	$t->album( $album );
	$t->year( $year );

	$t->write_tag() || next;

	# TODO: ID3v2?
}

